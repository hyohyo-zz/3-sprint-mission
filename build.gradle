plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.sprint.mission'
version = '2.2-M11'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.6'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.h2database:h2'

    implementation 'software.amazon.awssdk:s3:2.31.7'

    // MIME 타입 판별
    implementation 'org.apache.tika:tika-core:2.9.1'

    // .env 사용 위해 dotenv 의존성 주입
    testImplementation 'io.github.cdimascio:dotenv-java:2.3.0'

    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api:3.1.0'

    implementation 'com.nimbusds:nimbus-jose-jwt:10.3'
    implementation 'org.springframework.retry:spring-retry'

    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine'
    implementation 'org.springframework.kafka:spring-kafka'
}

// querydsl Q클래스 생성 경로설정
def querydslDir = layout.buildDirectory.dir("generated/sources/annotationProcessor/java/main").get().asFile

sourceSets {
    main {
        java {
            srcDir querydslDir
        }
    }
}

compileJava {
    options.annotationProcessorPath = configurations.annotationProcessor
    doFirst {
        querydslDir.mkdirs()
    }
}

// Clean 작업에서 생성된 Q클래스 제거
clean {
    delete querydslDir
}

configurations {
    querydsl.extendsFrom compileClasspath
    annotationProcessor
}

tasks.named('test') {
    useJUnitPlatform()
    ignoreFailures = true  // 실패해도 다음 task 진행
    finalizedBy(jacocoTestReport)  // 테스트 후 리포트 생성
    jvmArgs += ["-Duser.timezone=UTC"]
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/DiscodeitApplication.class",
                    "**/config/**",
                    "**/dto/**",
                    "**/entity/**"
            ])
        }))
    }
}

